[ compile, install and run program ]
dragon.just_run(program.sources !dragon.list, program.input !dragon.buffer)(compilation.error !dragon.error, program.result !dragon.buffer, program.error !dragon.buffer) = {
    [ get buffer of buffers ]
    dragon.list.calculate.content_buffer(program.sources)(sources.buffer)

    [ compile ]
    dragon.compiler.configure.default_compile()(settings)
    dragon.compiler.compile(sources.buffer, settings)(byte_code, debug_information, built_in_compilation_error)

    [ check for compilation error ]
    @print_compilation_error built_in_compilation_error:occured = {
        [ generate error ]
        dragon.error.generate(built_in_compilation_error:message, built_in_compilation_error:location)(compilation.error)

        [ skip program instalation and execution ]
        dragon.jump.bottom(dragon.always, @dragon.master_scope)()
    }

    [ open new context & install ]
    dragon.context.open()(context)
    dragon.context.install_program(context, byte_code)()

    [ pass input ]
    dragon.context.set.context_input(context, program.input)()

    [ run context ]
    dragon.set(dragon.integer.n1)(instruction_count)
    dragon.context.run(context, instruction_count)()

    [ get outputs and results ]
    dragon.context.get.context_result(context)(program.result)
    dragon.context.get.context_error(context)(program.error)

    [ clean up context ]
    dragon.buffer.return(context)()

    [ clean up program ]
    dragon.buffer.return(byte_code)()
}

[ test one file ]
dragon.just_run.one_file(file_path !dragon.buffer, program.input !dragon.buffer)(compilation.error !dragon.error, program.result !dragon.buffer, program.error !dragon.buffer) = {
    [ load file ]
    dragon.file_to_buffer(file_path)(user_code)

    [ open list ]
    dragon.structure.byte_size(user_code)(files.byte_size)
    dragon.list.open(files.byte_size)(files)

    [ append buffer ]
    dragon.list.append.buffer(files, user_code)(files)

    [ run test ]
    dragon.just_run(files, program.input)(compilation.error, program.result, program.error)

    [ clean up file ]
    dragon.buffer.return(user_code)()

    [ clean up files ]
    dragon.list.close(files)()
}
